plugins {
	id 'com.github.johnrengelman.shadow' version '7.0.0'
	id 'fabric-loom' version '0.9-SNAPSHOT'
}

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

configurations {
	shade
}

repositories {
	mavenCentral()
	maven {
		name 'AperLambda'
		url 'https://aperlambda.github.io/maven'
	}
	maven {
		name 'Gegy'
		url 'https://maven.gegy.dev'
	}
	maven { url 'https://jitpack.io' }
}

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:$minecraft_version"
	mappings "net.fabricmc:yarn:$yarn_mappings:v2"
	modImplementation "net.fabricmc:fabric-loader:$loader_version"

	modImplementation "net.fabricmc.fabric-api:fabric-api:$fabric_version"

	modImplementation "dev.lambdaurora:spruceui:$spruceui_version"
	include "dev.lambdaurora:spruceui:$spruceui_version"


	implementation "net.java.dev.jna:jna:$jna_version"
	shade implementation( "net.openhft:chronicle-queue:$cqueue_version" )
	implementation "blue.endless:jankson:$jankson_version"
	implementation "com.github.caplogic:mappedbus:$mappedbus_version"
}

shadowJar {
	configurations = [ project.configurations.shade ]
}

processResources {
	inputs.property 'version', project.version

	duplicatesStrategy = DuplicatesStrategy.INCLUDE

	filesMatching('fabric.mod.json') {
		expand 'version': project.version
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present. If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

jar {
	from 'LICENSE'
}
